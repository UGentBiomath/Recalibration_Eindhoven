/*******************************************************************************
 *
 * Tornado - Advanced Kernel for Modeling and Virtual Experimentation
 * (c) Copyright 2004-2011 DHI
 *
 * This file is provided under the terms of a license and may not be
 * distributed and/or modified except where allowed by that license.
 *
 * This file is provided as is with no warranty of any kind, including the
 * warranty of design, merchantability and fitness for a particular purpose.
 *
 * $Revision: 1$
 * $Date: 2. december 2016 14:37:29$
 *
 ******************************************************************************/

#ifndef SEWER_SPLITTERS_COMBINERS_SEWER_ABSTHREESPLITTER
#define SEWER_SPLITTERS_COMBINERS_SEWER_ABSTHREESPLITTER


CLASS SewAbsThreeSplitter
  (* icon = "three_flow_splitter" *)
  "3-way absolute splitter (pump) for sewer"
  EXTENDS Sewer_AbsTwoSplitter_Base WITH
  {:

  comments <- "3-way absolute splitter (pump) for sewer";

  interface <-
  {
   OBJ Outflow3 (* terminal = "out_3" *) "Outflow No.3" : OutSewTerminal := {: causality <- "COUT" ; group <- "Effluent 3" :};
   OBJ Q_Out3 (* terminal = "in_2"; manip = "1"; is_favorite = "1" *) "Flow rate for outflow No.3" : FlowRate := {: causality <- "CIN" ; value <- 20000; group <- "Operational" :};
  };

  state <-
  {
   OBJ f_Out1 (* hidden = "1" *) "Fraction of the influent flux going to outflow1" : Fraction ;
   OBJ f_Out2 (* hidden = "1" *) "Fraction of the influent flux going to outflow2" : Fraction ;
   OBJ f_Out3 (* hidden = "1" *) "Fraction of the influent flux going to outflow3" : Fraction ;
   OBJ Q_Out_3 (* is_favorite = "1" *) "Actual flow rate No.3" : FlowRate := {: group <- "Operational" :};
  };

  equations <-
  {
   state.Q_Out_1 = state.Q_In - (state.Q_Out_2 + state.Q_Out_3);
 
   state.Q_Out_2 = IF(interface.Q_Out2 > state.Q_In) THEN state.Q_In
   		ELSE interface.Q_Out2 ;
 
   state.Q_Out_3 = IF((interface.Q_Out2 + interface.Q_Out3) > state.Q_In) THEN (state.Q_In - state.Q_Out_2)
   		ELSE interface.Q_Out3 ;

   state.f_Out1 = 1 - (state.f_Out2 + state.f_Out3) ;
   
   state.f_Out2 = IF (state.Q_In == 0.0) THEN 0.0
   		ELSE state.Q_Out_2 / state.Q_In ;

   state.f_Out3 = IF (state.Q_In == 0.0) THEN 0.0
   		ELSE state.Q_Out_3 / state.Q_In ;

   interface.Outflow1[H2O] = - state.Q_Out_1 / 1E-06 ;
   
   interface.Outflow2[H2O] = - state.Q_Out_2 / 1E-06 ;

   interface.Outflow3[H2O] = - state.Q_Out_3 / 1E-06 ;

   {FOREACH Comp_Index IN {2 .. Kosim1NrOfComponents}: interface.Outflow1[Comp_Index] =
		- state.InFluxPerComponent[Comp_Index] * (state.f_Out1) ;};
 
   {FOREACH Comp_Index IN {2 .. Kosim1NrOfComponents}: interface.Outflow2[Comp_Index] =
   		- state.InFluxPerComponent[Comp_Index] * (state.f_Out2) ;};

   {FOREACH Comp_Index IN {2 .. Kosim1NrOfComponents}: interface.Outflow3[Comp_Index] =
   		- state.InFluxPerComponent[Comp_Index] * (state.f_Out3) ;};
  };

  :};

#endif // SEWER_SPLITTERS_COMBINERS_SEWER_ABSTHREESPLITTER