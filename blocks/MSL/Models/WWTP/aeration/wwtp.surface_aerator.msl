/*******************************************************************************
 *
 * Tornado - Advanced Kernel for Modeling and Virtual Experimentation
 * (c) Copyright 2004-2011 DHI
 *
 * This file is provided under the terms of a license and may not be
 * distributed and/or modified except where allowed by that license.
 *
 * This file is provided as is with no warranty of any kind, including the
 * warranty of design, merchantability and fitness for a particular purpose.
 *
 * $Revision: 1$
 * $Date: 30. september 2019 12:19:55$
 *
 ******************************************************************************/
 
#ifndef WWTP_SURFACE_AERATOR
#define WWTP_SURFACE_AERATOR

// The model computes the Kla values that are needed in the ASM models.
CLASS Surface_Aerator (* icon = "aerator" *)
"Basic model for surface aeration"
SPECIALISES PhysicalDAEModelType :=
  {:
  
  comments <- "Surface aeration model";
  interface <-
  {
  OBJ O2_tank (* terminal = "in_1" *) "DO concentration in the aeration tank" : Concentration := {: causality <- "CIN" ; group <- "Measurement data" :};
  OBJ Kla (* terminal = "out_1" *) "Computed Oxygen Transfer Coefficient" : OxygenTransferCoefficient := {: causality <- "COUT" ; group <- "Control action" :};
  OBJ Vol_tank (* terminal = "in_2"; manip = "1" *) "Volume of the aeration tank" : Volume := {: causality <- "CIN" ; value <- 1000 ; group <- "Measurement data" :};
  OBJ P (* terminal = "in_2"; manip = "1" *) "Power input": Power := {: causality <- "CIN" ; value <- 500000.0 ; group <- "Operational" :};
  OBJ T_water (* terminal = "in_2"; manip = "1" *) "Water temperature": CelsiusTemperature := {: causality <- "CIN" ; value <- 15.0 ; group <- "Local conditions" :};
  };

  parameters <-
  {
  OBJ AE "Aeration efficiency (kgO2/kWh)": Real := {: value <- 0.8; group <- "Energy" :};
  };
  
  independent <-
  {
  OBJ t "Time" : Time := {: group <- "Time" :};
  };
  
  state <-
  {
  OBJ O2_sat "Oxygen saturation (water temperature-dependent)": Concentration := {: group <- "Aeration" :};
  OBJ AerationEnergy "Energy consumption for aeration, integral" : ElectricalEnergy := {: group <- "Energy" :} ;
  };
  
  equations <-
  {
  state.O2_sat = 14.65 - 0.41 * interface.T_water + 0.00799 * pow(interface.T_water,2) - 0.0000778 * pow(interface.T_water,3) ;
  DERIV(state.AerationEnergy, [independent.t]) = interface.P / 1000 * 24;
  interface.Kla = parameters.AE * interface.P / (state.O2_sat - interface.O2_tank) / interface.Vol_tank * 24;
  };
  
  :};

#endif // WWTP_SURFACE_AERATOR
