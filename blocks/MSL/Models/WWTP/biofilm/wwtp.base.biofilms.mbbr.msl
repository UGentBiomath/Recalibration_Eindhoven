/*******************************************************************************
 *
 * Tornado - Advanced Kernel for Modeling and Virtual Experimentation
 * (c) Copyright 2004-2011 DHI
 *
 * This file is provided under the terms of a license and may not be
 * distributed and/or modified except where allowed by that license.
 *
 * This file is provided as is with no warranty of any kind, including the
 * warranty of design, merchantability and fitness for a particular purpose.
 *
 * $Revision: 1$
 * $Date: 30. september 2019 12:19:56$
 *
 ******************************************************************************/

#ifndef BASE_BIOFILM_MBBR
#define BASE_BIOFILM_MBBR

CLASS MBBR (* icon = "mbbr"; is_default = "true" *)
 "A model for an MBBR unit, based on the biofilm model by Van Hulle and Vanrolleghem"
 EXTENDS BaseBiofilm1D WITH
 {:
    
    comments <- "A model for an MBBR unit. Detachment when d>dmax and proportional to (d-dmax)^2";
    interface <-
    {
   OBJ MixingEnergy (* terminal = "out_2" *)"Mixing energy" : ElectricalEnergy := {: causality <- "COUT"; group <- "Energy" :};
    OBJ MixingPower (* terminal = "out_2" *) "Mixing power" : Real := {: causality <- "COUT"; unit <- "kWh/d" ; group <- "Energy" :};
    // FAPO 8/8/19
    OBJ OUR_bf (* terminal = "out_2" *) "Oxygen Uptake Rate in biofilm" : Real := {: causality <- "COUT"; unit <- "g/(m3.d)" ; group <- "Measurement data" :};
    OBJ OUR_tot (* terminal = "out_2" *) "Oxygen Uptake Rate in biofilm and bulk phase" : Real := {: causality <- "COUT"; unit <- "g/(m3.d)" ;group <- "Measurement data" :};
    };
    parameters <- 
    {  
       OBJ f_solid "Fraction of the solid support to the total volume of the reactor" : Ratio := {:value <- 0.05 ; group <- "Dimension" :} ;
     OBJ A_Sp_C "(carrier) Specific surface area" : SpecificAreaVolume := {:value <- 400 ; group <- "System" :};
// MIXING ENERGY
  OBJ ME_unit "Energy requirement per unit of volume for mixing in a AS tank" : Real := {: value <- 0.005; group <- "Mixing energy":};
  OBJ Kla_Min "Lowest kLa value that ensures adequate mixing" : OxygenTransferCoefficient :={: value <- 20.0; group <- "Mixing energy":};
    OBJ Mixing_When_Aerated "Mixing activity during aeration" : Integer := {: value <- 0 ; interval <- {: lowerBound <- 0; upperBound <- 1:}; group <- "Mixing energy":};
    };
    
      state <-
    {
// MIXING ENERGY
    OBJ Integ_ME (* hidden = "1" *)"Integral mixing energy" : ElectricalEnergy ;
  OBJ ME_Instant (* hidden = "1" *)"Instantaneous mixing energy" : Real:= {: group <- "Mixing":};
    };
     
    initial <-
    {
     // the apparent and true volume of the solid support are the same, in a IFAS
     parameters.fc = parameters.f_solid ;
     parameters.fc_real = parameters.f_solid ;
     parameters.area_sp = parameters.A_Sp_C ;
     //
     //Volume of the reactor
     parameters.V_R = parameters.L * parameters.W * parameters.H ;
     parameters.Vc_real = parameters.fc_real * parameters.V_R ;
     parameters.Vc = parameters.fc * parameters.V_R ;
     // Surface of the biofilm
     parameters.A = parameters.area_sp * parameters.Vc ;
     //
     //rho = f(d)
     {FOREACH Layer_Index IN {1 .. NrOfLayers}:
          parameters.rho_layer[Layer_Index]=parameters.eta*parameters.rho ; };
     //
     state.M[IndexOfSolvent] = ((parameters.V_R - parameters.Vc_real) - (parameters.A*parameters.d_ini)) / parameters.WWTPSpecificVolume[IndexOfSolvent] ;

   state.Integ_ME = 0.0 ;
 };

    equations <-
    {
    // ... ... CALCULATION OF THE DETACHMENT ... ...
    {FOREACH Layer_Index IN {1 .. NrOfLayers} : state.u_Dt[Layer_Index] =
           IF ( state.d_layer_calc[Layer_Index] <= (parameters.d_max/NrOfLayers) )
            THEN 0.0
          ELSE parameters.k_detach * pow(( state.d_layer_calc[Layer_Index] - (parameters.d_max/NrOfLayers) ),2) ; 
       };

// MIXING ENERGY
  state.ME_Instant = IF ((state.Kla_Actual < parameters.Kla_Min) || (state.Kla_Actual == 0))
            THEN parameters.ME_unit * state.V
          ELSE 
            IF (parameters.Mixing_When_Aerated == 1)
            THEN parameters.ME_unit * state.V
            ELSE 0;   

    //(EURE 25/07/2019) [kWh/d]
    interface.MixingPower = state.ME_Instant ;
  DERIV(state.Integ_ME,[independent.t])= state.ME_Instant;
  interface.MixingEnergy = state.Integ_ME ;
    
    // (FAPO 8/8/2019) OUR calculation including oxygen uptake in biofilm      
    interface.OUR_bf = - 1 * (SUMOVER Layer_Index IN {1 .. NrOfLayers}: state.Reaction_Term_Biofilm[S_O][Layer_Index]); 
    interface.OUR_tot = interface.OUR_ASU + interface.OUR_bf ;
          
    }; 

 :};

#endif // BASE_BIOFILM_MBBR
